buildscript {

    ext.kotlin_version = "1.3.72"
    ext.coroutines_version = "1.3.5"
    ext.junit_version = "4.12"
    ext.library_core_version = "0.3.2.7"
    ext.library_android_version = "0.3.2.7"
    ext.library_android_compat_version = "0.3.2.7"

    repositories {
        google()
        gradlePluginPortal()
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:4.0.1'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jetbrains.kotlinx:kotlinx-metadata-jvm:0.1.0"
    }
}

Properties awsCredentials = new Properties()
File awsCredentialsFile = file("../aws.credentials")
if (awsCredentialsFile.exists()) {
    awsCredentials.load(awsCredentialsFile.newDataInputStream())
}

allprojects {
    repositories {
        maven {
            url "s3://maven.apptronic.net/"
            credentials(AwsCredentials) {
                accessKey awsCredentials.getProperty("accessKey", "")
                secretKey awsCredentials.getProperty("secretKey", "")
            }
        }
        google()
        mavenCentral()
        jcenter()
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

task bundleLibrary(dependsOn: [
        ':library:bundleDebugAar',
        ':library:sourceJar',
])

task bundleCompatLibrary(dependsOn: [
        ':library_compat:bundleDebugAar',
        ':library_compat:sourceJar',
])

task publishLibrary(dependsOn: [
        ':library:publish',
])

task publishCompatLibrary(dependsOn: [
        ':library_compat:publish',
])

task publishLibraryToMavenLocal(dependsOn: [
        ':library:publishToMavenLocal',
        ':library_compat:publishToMavenLocal',
])

task publishLibToMavenApptronicNet() {
    dependsOn 'clean'
    dependsOn 'bundleLibrary'
    dependsOn 'publishLibrary'
    tasks.findByName('bundleLibrary').mustRunAfter 'clean'
    tasks.findByName('publishLibrary').mustRunAfter 'bundleLibrary'
}

task publishLibToMavenLocal() {
    dependsOn 'clean'
    dependsOn 'bundleLibrary'
    dependsOn 'publishLibraryToMavenLocal'
    tasks.findByName('bundleLibrary').mustRunAfter 'clean'
    tasks.findByName('publishLibraryToMavenLocal').mustRunAfter 'bundleLibrary'
}