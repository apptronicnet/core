buildscript {
    ext.kotlin_version = "1.4.10"
    ext.coroutines_version = "1.3.9"
    ext.junit_version = "4.12"
    ext.library_core_version = "0.7.0"
    repositories {
        gradlePluginPortal()
        google()
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jetbrains.kotlin:kotlin-serialization:$kotlin_version"
        classpath 'com.android.tools.build:gradle:4.0.1'
    }
}

apply plugin: "kotlin-multiplatform"
apply plugin: "kotlinx-serialization"
apply plugin: "maven-publish"
apply plugin: "com.android.library"
apply plugin: "kotlin-android-extensions"

repositories {
    gradlePluginPortal()
    google()
    jcenter()
    mavenCentral()
}

group = "net.apptronic.core"
version = ext.library_core_version

kotlin {
    jvm()
    android()
    iosX64('ios') {
        binaries {
            framework {
                baseName = 'CoreLibraryCommon'
            }
        }
    }
    sourceSets {
        commonMain {
            dependencies {
                implementation kotlin("reflect")
                implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutines_version"
            }
        }
        jvmMain {
            dependencies {
                implementation "org.jetbrains.kotlinx:kotlinx-coroutines-jdk8:$coroutines_version"
            }
        }
        jvmTest {
            dependencies {
                implementation kotlin("test-junit")
                implementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:$coroutines_version"
            }
        }
        androidMain {
            dependencies {
                implementation "androidx.core:core:1.3.1"
                implementation "androidx.core:core-ktx:1.2.0"
                implementation "androidx.appcompat:appcompat:1.2.0"
                implementation "com.google.android.material:material:1.2.1"
                implementation "androidx.recyclerview:recyclerview:1.1.0"
            }
        }
        androidTest {

        }
        iosMain {
            dependencies {
                implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core-iosx64:$coroutines_version"
            }
        }
    }
    targets {
        configure([]) {
            tasks.getByName(compilations.main.compileKotlinTaskName).kotlinOptions {
                jvmTarget = "1.8"
            }
        }
    }
}

android {
    compileSdkVersion 30
    defaultConfig {
        minSdkVersion 23
        targetSdkVersion 30
        versionCode 1
        versionName '1.0'
    }
    buildTypes {
        debug {
            minifyEnabled false
        }
    }
}

Properties awsCredentials = new Properties()
File awsCredentialsFile = file("../aws.credentials")
if (awsCredentialsFile.exists()) {
    awsCredentials.load(awsCredentialsFile.newDataInputStream())
}

publishing {
    publications {
        jvm {
            groupId = "net.apptronic.core"
            artifactId = "core-commons"
            version = project.ext.library_core_version
        }
    }
    repositories {
        maven {
            url "s3://maven.apptronic.net/"
            credentials(AwsCredentials) {
                accessKey awsCredentials.getProperty("accessKey", "")
                secretKey awsCredentials.getProperty("secretKey", "")
            }
        }
    }
}

task publishCoreLibrary {
    dependsOn 'publish'
    doLast {
        println("Invalidation path for CloudFront:")
        println("/net/apptronic/core/core-commons/$library_core_version/*")
    }
}

def copyFrameworkTo(File targetDir) {
    def srcFile = kotlin.targets."$target".binaries.getFramework(buildType).outputFile
    delete fileTree(dir: targetDir)
    targetDir.mkdirs()
    copy {
        from srcFile.parent
        into targetDir
        include 'CoreLibraryCommon.framework/**'
        include 'CoreLibraryCommon.framework.dSYM'
    }
}

task copyFramework {
    def buildType = project.findProperty('kotlin.build.type') ?: 'DEBUG'
    def target = project.findProperty('kotlin.target') ?: 'ios'
    dependsOn kotlin.targets."$target".binaries.getFramework(buildType).linkTask

    doLast {
        def srcFile = kotlin.targets."$target".binaries.getFramework(buildType).outputFile
        def targetDir = rootProject.file("../core_library_ios/ApptronicNetCoreSampleApp/CoreLibraryCommon")
        delete fileTree(dir: targetDir)
        targetDir.mkdirs()
        copy {
            from srcFile.parent
            into targetDir
            include 'CoreLibraryCommon.framework/**'
            include 'CoreLibraryCommon.framework.dSYM'
        }
    }
}