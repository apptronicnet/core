buildscript {
    ext.kotlin_version = "1.4.0"
    ext.coroutines_version = "1.3.9"
    ext.junit_version = "4.12"
    ext.library_core_version = "0.6.0.0-dev1"
    repositories {
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jetbrains.kotlin:kotlin-serialization:$kotlin_version"
    }
}

apply plugin: "kotlin-multiplatform"
apply plugin: "kotlinx-serialization"
apply plugin: "maven-publish"

repositories {
    google()
    gradlePluginPortal()
    mavenCentral()
    jcenter()
}

wrapper {
    gradleVersion = "6.1.1"
    distributionType = "ALL"
}

group = "net.apptronic.core"
version = ext.library_core_version

kotlin {
    jvm()
    ios {
        binaries {
            framework()
        }
    }
    sourceSets {
        commonMain {
            dependencies {
//                implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
                implementation kotlin("reflect")
                implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutines_version"
            }
        }
        commonTest {
            dependencies {
                implementation kotlin("test-common")
                implementation kotlin("test-annotations-common")
            }
        }
        jvmMain {
            dependencies {
                implementation "org.jetbrains.kotlinx:kotlinx-coroutines-jdk8:$coroutines_version"
            }
        }
        jvmTest {
            dependencies {
                implementation kotlin("test-junit")
                implementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:$coroutines_version"
            }
        }
        ios {
            dependencies {
                implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core-iosx64:$coroutines_version"
            }
        }
    }
    targets {
        configure([]) {
            tasks.getByName(compilations.main.compileKotlinTaskName).kotlinOptions {
                jvmTarget = "1.8"
            }
        }
    }
}

Properties awsCredentials = new Properties()
File awsCredentialsFile = file("../aws.credentials")
if (awsCredentialsFile.exists()) {
    awsCredentials.load(awsCredentialsFile.newDataInputStream())
}

publishing {
    publications {
        jvm {
            groupId = "net.apptronic.core"
            artifactId = "core-commons"
            version = project.ext.library_core_version
        }
    }
    repositories {
        maven {
            url "s3://maven.apptronic.net/"
            credentials(AwsCredentials) {
                accessKey awsCredentials.getProperty("accessKey", "")
                secretKey awsCredentials.getProperty("secretKey", "")
            }
        }
    }
}

task publishCoreLibrary {
    dependsOn 'publish'
    doLast {
        println("Invalidation path for CloudFront:")
        println("/net/apptronic/core/core-commons/$library_core_version/*")
    }
}

//task copyFramework {
//    def buildType = project.findProperty('kotlin.build.type') ?: 'DEBUG'
//    def target = project.findProperty('kotlin.target') ?: 'ios'
//    dependsOn kotlin.targets."$target".binaries.getFramework(buildType).linkTask
//
//    doLast {
//        def srcFile = kotlin.targets."$target".binaries.getFramework(buildType).outputFile
//        def targetDir = rootProject.file("../lib_ios/framework")
//        delete fileTree(dir: targetDir)
//        targetDir.mkdirs()
//        copy {
//            from srcFile.parent
//            into targetDir
//            include 'core_library_common.framework/**'
//            include 'core_library_common.framework.dSYM'
//        }
//    }
//}